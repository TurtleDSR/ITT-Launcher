plugins {
  id 'java'
  id 'application'
  id 'org.beryx.runtime' version '1.13.1'
}

java {
  toolchain { languageVersion = JavaLanguageVersion.of(11) }
}

application {
  mainClass = 'com.turtledsr.launcher.Main'
}

afterEvaluate {
  runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.base', 'java.desktop', 'java.logging']
  }
}

sourceSets {
  java11 {
    java { srcDirs = ['src/java11'] }
    resources { srcDirs = ['src/java11/resources'] }
  }
  main {
    java { srcDirs = ['src/main'] }
  }
}

dependencies {
  implementation fileTree(dir: 'lib', include: ['*.jar'])
  java11Implementation fileTree(dir: 'lib', include: ['*.jar'])
}

tasks.named('compileJava') {
  options.release = 8
}

tasks.named('compileJava11Java') {
  options.release = 11
}

tasks.named('processJava11Resources') {
  filesMatching(['**/*.ttf', '**/*.otf', '**/*.zip']) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
  }
}

jar {
  manifest {
    attributes('Multi-Release': 'true', 'Main-Class': 'com.turtledsr.launcher.Main')
  }
  into('META-INF/versions/11') {
    from sourceSets.java11.output
  }

  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  } {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  }
}

tasks.register('buildRuntime', Exec) {
  dependsOn jar
  
  def jreFolder = layout.buildDirectory.dir("jre").get().asFile
  
  doFirst {
    if (jreFolder.exists()) {
      jreFolder.deleteDir()
    }
  }

  def jlinkPath = javaToolchains.compilerFor(java.toolchain).get()
    .metadata.installationPath.file("bin/jlink").asFile.absolutePath
  commandLine jlinkPath, 
    '--module-path', "${javaToolchains.compilerFor(java.toolchain).get().metadata.installationPath.asFile}/jmods",
    '--add-modules', 'java.base,java.desktop,java.logging',
    '--strip-debug', 
    '--compress', '2', 
    '--no-header-files', 
    '--no-man-pages', 
    '--output', jreFolder.absolutePath
}